module til-typecheck-stats
imports til-typecheck-var
strategies

  typecheck-block = 
    Block(typecheck-stats)

  typecheck-stats = 
    {| TypeOf : map(debug(!"in: "); typecheck-stat; debug(!"out: ")) |}

  typecheck-stat = 
    typecheck-block
    <+ Assign(id, typecheck-exp); try(TypecheckAssign)
    <+ typecheck-declaration
    <+ IfElse(typecheck-exp, typecheck-stats, typecheck-stats); typecheck-if
    <+ IfThen(typecheck-exp, typecheck-stats); typecheck-if
    <+ While(typecheck-exp, typecheck-stats); typecheck-while
    <+ ProcCall(id, map(typecheck-exp)); typecheck-proccall
    <+ For(id, typecheck-exp, typecheck-exp, typecheck-stats); typecheck-for
    <+ debug(!"unknown statement: ")
       ; <exit> 1

  typecheck-if :
    IfElse(e, st1*, st2*) -> IfElse(e, st1*, st2*){TypeName("void")}
    where <typeof> e => TypeName("bool")
    
  typecheck-if :
    IfThen(e, st*) -> IfThen(e, st*){TypeName("void")}
    where <typeof> e => TypeName("bool")

  typecheck-while : 
    While(e, st*) -> While(e, st*){TypeName("void")}
    where <typeof> e => TypeName("bool")

  typecheck-for : 
    For(x, e1, e2, st*) -> For(x, e1, e2, st*){TypeName("void")}
    where <TypeOf> x => TypeName("int")
        ; <typeof> e1 => TypeName("int")
        ; <typeof> e2 => TypeName("int")
    